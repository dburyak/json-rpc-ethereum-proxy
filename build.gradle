plugins {
    id 'java'
    id 'application'
    id 'jvm-test-suite'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

configurations {
    compileOnly { extendsFrom annotationProcessor }
}

dependencies {
    // In a more complex project versions would be defined and
    // managed differently. For a simple case this will suffice.
    def vertxVersion = '5.0.4'
    implementation "io.vertx:vertx-core:$vertxVersion"
    implementation "io.vertx:vertx-rx-java3:$vertxVersion"
    implementation "io.vertx:vertx-web:$vertxVersion"
    implementation "io.vertx:vertx-web-client:$vertxVersion"
    implementation "io.vertx:vertx-config:$vertxVersion"
    implementation "io.vertx:vertx-config-yaml:$vertxVersion"
    implementation "io.vertx:vertx-redis-client:$vertxVersion"

    annotationProcessor 'org.projectlombok:lombok:1.18.40'
    implementation platform('org.apache.logging.log4j:log4j-bom:2.25.1')
    implementation 'org.apache.logging.log4j:log4j-core'
    runtimeOnly 'com.fasterxml.jackson.core:jackson-databind'
    runtimeOnly 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    implementation 'com.github.ben-manes.caffeine:caffeine:3.2.2'

    // testing
    testImplementation 'org.assertj:assertj-core:3.27.4'
}

application {
    mainClass = 'com.dburyak.exercise.jsonrpc.App'
    applicationDefaultJvmArgs = [
            '-Dvertx.logger-delegate-factory-class-name=io.vertx.core.logging.Log4j2LogDelegateFactory'
    ]
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

dependencyLocking {
    lockAllConfigurations()
}

// https://docs.gradle.org/current/userguide/dependency_locking.html#lock_all_configurations_in_one_build_execution
tasks.register('updateLockedVersions') {
    notCompatibleWithConfigurationCache('Filters configurations at execution time')
    doFirst {
        assert gradle.startParameter.writeDependencyLocks:
                "$path must be run from the command line with the `--write-locks` flag"
    }
    doLast {
        configurations.findAll {
            // Add any custom filtering on the configurations to be resolved
            it.canBeResolved
        }.each { it.resolve() }
    }
}

wrapper {
    gradleVersion = project.property('org.gradle.version')
}

tasks.named('prepareKotlinBuildScriptModel') {
    enabled = false
}
